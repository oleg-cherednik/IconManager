buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenLocal()
    mavenCentral()
}

group 'ru.oleg-cherednik.icoman'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'unknown'

configurations {
    jar.archiveName = "icon-manager-${version}.jar"
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-collections:commons-collections:3.2.2'
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'commons-io:commons-io:2.4'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'ch.qos.logback:logback-core:1.1.7'
    compile 'org.assertj:assertj-core:3.5.2'

    compileOnly 'org.projectlombok:lombok:1.16.10'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.11.1'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

task demo(type: Copy) {
    description 'Copy demo to separate folder.'

    from 'build/libs'
    into 'demo'
    include "**/$jar.baseName-$project.version" + '.jar'
}

modifyPom {
    project {
        name 'IconManager'
        description 'Icon Manager (ico, icl, icns).'
        url 'https://github.com/oleg-cherednik/IconManager'
        inceptionYear '2018'

        scm {
            url 'https://github.com/oleg-cherednik/IconManager'
            connection 'scm:https://github.com/oleg-cherednik/IconManager.git'
            developerConnection 'scm:git@github.com:oleg-cherednik/IconManager.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'oleg.cherednik'
                name 'Oleg Cherednik'
                email 'oleg.cherednik@gmail.com'
            }
        }
    }
}

extraArchive {
    sources true
    tests true
    javadoc false
}

nexus {
    sign true
    repositoryUrl 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl 'https://oss.sonatype.org/content/repositories/snapshots'
}

nexusStaging {
    packageGroup 'ru.oleg-cherednik'
    stagingProfileId 'D3835BF60F05B9'
}

